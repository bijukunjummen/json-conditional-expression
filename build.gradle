buildscript {
    ext {
        springBootVersion = '2.2.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
    id 'maven'
    id 'nebula.release' version '13.0.0'
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
}

group = 'com.github.bijukunjummen'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
    commonsCodecVersion="1.11"
    jacksonBom="2.9.10.20191020"
    junitVersion="5.3.2"
}
repositories {
    jcenter()
}

dependencies {
    testImplementation enforcedPlatform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    implementation enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jacksonBom}")
    api "commons-codec:commons-codec:$commonsCodecVersion"
    api "com.fasterxml.jackson.core:jackson-core"
    api "com.fasterxml.jackson.core:jackson-databind"
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api("org.jetbrains.kotlin:kotlin-reflect")
    api("org.jetbrains.kotlin:kotlin-stdlib")
    api("org.jetbrains.kotlin:kotlin-stdlib-common")
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

bintray {
    user = bintray_user
    key = bintray_api_key
    configurations = ['archives']
    pkg {
        repo = 'repo'
        name = project.name
        group = project.group
//        version = project.version
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bijukunjummen/json-conditional-expression.git'
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'json-conditional-expression'
            description 'Utility for expressing and evaluating a conditional expression in Java'
            url 'https://github.com/bijukunjummen/json-conditional-expression.git'
            inceptionYear '2020'

            packaging 'jar'
            groupId project.group
            artifactId project.name
            version project.version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/bijukunjummen/json-conditional-expression.git'
                url 'https://github.com/bijukunjummen/json-conditional-expression.git'

            }
            developers {
                developer {
                    name 'Biju Kunjummen'
                }
            }
        }
    }
}
